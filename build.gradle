plugins {
    id 'org.springframework.boot' version '2.0.0.RELEASE'
    id "ajk.gradle.elastic" version "0.0.20"
}

repositories {
    mavenCentral()
    maven {
        url "https://artifacts.elastic.co/maven"
    }
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'uk.gov.cslearning'
version = '0.1.0-' + (System.getenv('TRAVIS_COMMIT') ?: 'local')

bootJar {
    archiveName = 'learning-catalogue.jar'
    mainClassName = 'uk.gov.cslearning.catalogue.Application'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    compile 'org.springframework.boot:spring-boot-starter-jetty'
    compile 'org.springframework.boot:spring-boot-starter-jersey'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-configuration-processor'
    compile('org.springframework.data:spring-data-elasticsearch') {
        exclude module: 'log4j-api'
    }
    compile 'com.google.guava:guava:23.5-jre'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'org.codehaus.janino:janino:3.0.8'
    compile 'com.github.vanroy:spring-boot-starter-data-jest:3.1.1.RELEASE'

    compile group: 'com.github.vanroy', name: 'spring-boot-starter-data-jest', version: '3.1.1.RELEASE'

    testCompile 'com.google.code.gson:gson:2.8.2'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

compileJava.dependsOn(processResources)

task integrationTest(type: Test) {
    reports {
        html.destination "$buildDir/reports/integration-test"
    }

    include "**/*IT.*"

    doFirst {
        startElastic {
            elasticVersion = "5.1.2"
            httpPort = 9998
            transportPort = 9999
            dataDir = file("$buildDir/elastic")
            logsDir = file("$buildDir/elastic/logs")
        }
    }

    doLast {
        stopElastic {
            httpPort = 9998
        }
    }
}

gradle.taskGraph.afterTask { Task task, TaskState taskState ->
    if (task.name == "integrationTest") {
        stopElastic {
            httpPort = 9998
        }
    }
}

test {
    include '**/*Test.*'
    exclude '**/*IT.*'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3'
}
